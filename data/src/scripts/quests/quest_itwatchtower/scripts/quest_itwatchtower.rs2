[oploc1,loc_2299]
if(stat(agility) < 18) {
    ~mesbox("You need an Agility level of 18 to climb this wall.");
    return;
}
mes("You climb up the wall...");
~agility_climb_up(310, 1_39_48_52_45);
mes("...and squeeze in through the window.");

[oploc1,loc_2798]
if(%itwatchtower_progress = ^itwatchtower_not_started) {
    ~chatplayer("<p,happy>I am not sure why I am searching this bush.");
} else {
    ~chatplayer("<p,happy>Hmmm, nothing here.");
}

[oploc1,loc_2799]
@itwatchtower_search_bush(fingernails, "<p,quiz>What's this? Disgusting! Some fingernails. They may be a clue though... I'd better take them.");

[oploc1,loc_2800]
@itwatchtower_search_bush(damaged_dagger, "<p,happy>Aha a dagger!|I wonder if this is evidence...");

[oploc1,loc_2801]
@itwatchtower_search_bush(tattered_eye_patch, "<p,happy>I've found an eye patch; I had better show this to the Watchtower Wizard.");

[oploc1,loc_2802]
@itwatchtower_search_bush(old_robe, "<p,happy>Aha! A robe.|This could be a clue...");

[oploc1,loc_2803]
@itwatchtower_search_bush(unusual_armour, "<p,happy>Here's some armour; it could be evidence...");

[label,itwatchtower_search_bush](namedobj $obj, string $str0) {
    if(%itwatchtower_progress = ^itwatchtower_not_started) {
        ~chatplayer("<p,happy>I am not sure why I am searching this bush.");
        return;
    }

    if (%itwatchtower_progress > ^itwatchtower_started) {
        ~chatplayer("<p,happy>Hmmm, nothing here.");
        return;
    }

    if (inv_total(inv, $obj) > 0) {
        ~chatplayer("<p,quiz>I have already searched this place.");
        return;
    }

    inv_add(inv, $obj, 1);
    ~chatplayer($str0);
}

[oploc1,loc_2833]
if(%itwatchtower_progress = ^itwatchtower_not_started) {
    ~chatnpc_specific(nc_name(tower_guard), tower_guard, "<p,angry>You can't go up there.|That's private that is.");
} else {
    ~chatnpc_specific(nc_name(tower_guard), tower_guard, "<p,neutral>It is the wizards' helping hand - let 'em up.");
    if_close;
    @climb_ladder(movecoord(coord, 0, 1, 0), true);
}

[oploc1,loc_2805]
@no_map_message;
p_teleport(0_39_47_27_62);

[oploc1,loc_2806]
@no_map_message;
p_teleport(0_39_47_44_47);

[oploc1,loc_2807]
@no_map_message;
p_teleport(0_39_47_57_47);

[oploc1,loc_2808]
@no_map_message;
p_teleport(0_39_47_56_26);

[oploc1,loc_2809]
@no_map_message;
p_teleport(0_40_47_3_16);

[label,no_map_message]
mes("There's no way I can find my way through without a map of some kind.");

[oploc1,loc_2810]
p_teleport(0_39_47_27_62);

[oploc1,loc_2790]
if(inv_total(inv, toban_key) > 0) {
    mes("You use the key Og gave you.");
    @open_toban_chest;
}
mes("The chest is locked.");
~chatplayer("<p,quiz>I think I need a key of some sort.");

[oplocu,loc_2790]
if(last_useitem = toban_key) {
    @open_toban_chest;
}
~displaymessage(^dm_default);

[label,open_toban_chest]
inv_del(inv, toban_key, 1);
loc_change(loc_3273, 20);
sound_synth(chest_open, 0, 0);
if(inv_total(inv, tobans_gold) > 0) {
    mes("You have already got the stolen gold.");
    return;
}
inv_add(inv, tobans_gold, 1);
~objbox(tobans_gold, "You find a stash of gold inside.", 250, 0, 0);

[oploc1,loc_2811]
mes("You enter the cave.");
p_teleport(0_40_47_16_21);
~chatplayer("<p,shock>Wow! That tunnel went a long way.");

[oploc1,loc_2812]
mes("You climb down the ladder.");
p_teleport(0_39_46_4_44);

[oploc1,loc_2796]
@climb_ladder(2_39_48_53_40, true);

[oploc1,loc_2797]
@climb_ladder(1_39_48_53_40, true);

[oploc1,loc_2786] @open_gutanoth_gate(^left, ogre_guard_east);
[oploc1,loc_2787] @open_gutanoth_gate(^right, ogre_guard_east);
[oploc1,loc_2788] @open_gutanoth_gate(^left, ogre_guard_west);
[oploc1,loc_2789] @open_gutanoth_gate(^right, ogre_guard_west);

[label,open_gutanoth_gate](int $side, npc $guard)
if(($guard = ogre_guard_east & %itwatchtower_gold < ^itwatchtower_found_gold) |  ($guard = ogre_guard_west & %itwatchtower_progress < 4)) {
    if(npc_find(coord, $guard, 6, 2) = true) {
        mes("A nearby guard calls you over.");
        p_opnpc(1);   
    }
    return;
}
~open_and_close_double_door2(~check_axis(coord, loc_coord, loc_angle), $side, grate_open); // iron_door_open

[oploc1,loc_2834]
~chatplayer("<p,confused>What's this? The bridge is out - I'll need to find another way in. I can see a ladder up there coming out of a hole; there must be a tunnel that leads here...");

[oplocu,loc_2326]
if(last_useitem = rope) {
    if(distance(coord, loc_coord) > 3) {
        mes("You need to be closer.");
        return;
    }
    ~forcewalk2(0_39_48_5_15);
    p_arrivedelay;
    facesquare(movecoord(coord, 4, 0, 0));
    mes("You tie the rope to the tree...");
    anim(seq_775, 0);
    p_delay(0);
    inv_del(inv, rope, 1);
    loc_change(loc_2324, 4);
    p_delay(0);
    loc_anim(rope_swing);
    ~agility_exactmove(human_ropeswing, 20, 0, coord, movecoord(loc_coord, 6, 0, 0), 45, 70, ^exact_east, false);
    mes("You skillfully swing across.");
    return;
}
mes("You see no way to do that.");